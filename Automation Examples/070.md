# Process

Extract all emails since 25.09.2019 date that its subject contains `#robottest` and display info about them (sender's email address, mail subject and the mail message).

# Steps to automate

##### 1. Log in to your mail account

Make sure to enable addon net from the View/Addons panel and that your mail credentials are correct.

```G1ANT
imap.open host ‴imap.gmail.com‴ login ‴user@gmail.com‴ password ‴p@$$w0rD‴ ignorecertificateerrors true
```

##### 2. Get mails since the correct date

```G1ANT
imap.getmails result ♥mails sincedate ‴25/09/2019‴
```

##### 3. Close mail connection

```G1ANT
imap.close
```

##### 4. Iterate through all the mails and display only those that match the criteria

```G1ANT
foreach ♥mail in ♥mails
    if ⊂♥mail⟦subject⟧.Contains("#robottest")⊃
        dialog ♥mail
    end if
end foreach
```

##### 5. Show sender's address, mail subject and mail body for all matching emails

Just change the code from the previous step a little. The `♥mail` variable is of mail structure and there are several indexes that you can access.

```G1ANT
foreach ♥mail in ♥mails
    if ⊂♥mail⟦subject⟧.Contains("#robottest")⊃
        dialog ♥mail⟦from⟧
        dialog ♥mail⟦subject⟧
        dialog ♥mail⟦content⟧
    end if
end foreach
```

# Final Code

```G1ANT
imap.open host ‴imap.gmail.com‴ login ‴user@gmail.com‴ password ‴p@$$w0rD‴ ignorecertificateerrors true
imap.getmails result ♥mails sincedate ‴25/09/2019‴
imap.close

foreach ♥mail in ♥mails
    if ⊂♥mail⟦subject⟧.Contains("#robottest")⊃
        dialog ♥mail⟦from⟧
        dialog ♥mail⟦subject⟧
        dialog ♥mail⟦content⟧
    end if
end foreach
```
