# Process

Extract all emails since 25.09.2019 date that their subject contains `#robottest` and insert info about them (sender's email address, mail subject and the mail message) into a .csv file.

# Steps to automate

##### 1. Log in to your mail account

Make sure to enable addon net from the View/Addons panel and that your mail credentials are correct.

```G1ANT
imap.open host ‴imap.gmail.com‴ login ‴user@gmail.com‴ password ‴p@$$w0rD‴ ignorecertificateerrors true
```

##### 2. Get mails since the correct date

```G1ANT
imap.getmails result ♥mails sincedate ‴25/09/2019‴
```

##### 3. Close mail connection

```G1ANT
imap.close
```

#### 4. Create empty .csv file

```G1ANT
♥path = ‴C:\Users\dell\Desktop\Automation Examples\Mails.csv‴
text.write filename ♥path text ‴‴
```

##### 4. Iterate through all the mails and display only those that match the criteria (for debugging purposes)

```G1ANT
foreach ♥mail in ♥mails
    if ⊂♥mail⟦subject⟧.Contains("#robottest")⊃
        dialog ♥mail
    end if
end foreach
```

##### 5. Insert sender's address, mail subject and mail body for all matching emails into the .csv file

Just change the code from the previous step a little. The `♥mail` variable is of mail structure and there are several indexes that you can access.

```G1ANT
foreach ♥mail in ♥mails
    if ⊂♥mail⟦subject⟧.Contains("#robottest")⊃
        text.write filename ♥path text ‴♥mail⟦from⟧, ♥mail⟦subject⟧, ♥mail⟦content⟧♥newline‴ writemode append
    end if
end foreach
```

# Final Code

```G1ANT
imap.open host ‴imap.gmail.com‴ login ‴user@gmail.com‴ password ‴p@$$w0rD‴ ignorecertificateerrors true
imap.getmails result ♥mails sincedate ‴25/09/2019‴
imap.close

♥path = ‴C:\Users\dell\Desktop\Automation Examples\Mails.csv‴
text.write filename ♥path text ‴‴

foreach ♥mail in ♥mails
    if ⊂♥mail⟦subject⟧.Contains("#robottest")⊃
        text.write filename ♥path text ‴♥mail⟦from⟧, ♥mail⟦subject⟧, ♥mail⟦content⟧♥newline‴ writemode append
    end if
end foreach
```
