# Process Description

Extract all emails since 25.09.2019 date that their subject contains `#robottest` and insert info about them (sender's email address, mail subject and the mail message) into a .csv file. Send an email with the attachment.

# Steps to Automate

### 1. Log in to your mail account

Make sure to enable addon net from the View/Addons panel. 

It is a good practice to store usernames and passwords in Credential Container. In order to add a credential, choose Tools/Credential Container..., click "Add" and in the "Key" field insert the name of your value, for example "mail:address". Remember to have a key containing the `:` sign. In the "Value" and "Retype value" fields, insert for example your mail address. To the same with a mail password and a host name if you like (for example: imap.gmail.com).

See below how to access those values using the `♥credential` special variable.

```G1ANT
imap.open host ♥credential⟦mail:host⟧ login ♥credential⟦mail:address⟧ password ♥credential⟦mail:password⟧ ignorecertificateerrors true
```

### 2. Get mails since the correct date

```G1ANT
imap.getmails result ♥mails sincedate ‴25/09/2019‴
```

### 3. Close the mail connection

```G1ANT
imap.close
```

### 4. Create an empty .csv file

Don't forget to add a C# snippet which will create the directory for you.

```G1ANT
♥path = ‴♥environment⟦HOMEDRIVE⟧♥environment⟦HOMEPATH⟧\Documents\G1ANT.Robot\Automation Examples\mails.csv‴
⊂
System.IO.Directory.CreateDirectory(System.IO.Path.GetDirectoryName(♥path));
⊃
text.write filename ♥path text ‴‴
```

### 5. Iterate through all mails and display only those that match the criteria (for debugging purposes)

```G1ANT
foreach ♥mail in ♥mails
    if ⊂♥mail⟦subject⟧.Contains("#robottest")⊃
        dialog ♥mail
    end if
end foreach
```

### 6. Insert sender's address, mail subject and mail body for all matching emails into the .csv file

Just change the code from the previous step a little. The `♥mail` variable is of mail structure and there are several indexes that you can access.

```G1ANT
foreach ♥mail in ♥mails
    if ⊂♥mail⟦subject⟧.Contains("#robottest")⊃
        text.write filename ♥path text ‴♥mail⟦from⟧, ♥mail⟦subject⟧, ♥mail⟦content⟧♥newline‴ writemode append
    end if
end foreach
```

### 7. Send your .csv file to yourself

Log in using the credentials that you used in the `imap.open` command.

```G1ANT
list.create text ♥path result ♥attachments
mail.smtp host ♥credential⟦mail:host⟧ login ♥credential⟦mail:address⟧ password ♥credential⟦mail:password⟧ from ♥credential⟦mail:address⟧ to ‴robotuser@g1ant.com‴ subject ‴Csv attachment‴ body ‴Hi, here's your mail info‴ attachments ♥attachments
```

# Final Code

```G1ANT
imap.open host ♥credential⟦mail:host⟧ login ♥credential⟦mail:address⟧ password ♥credential⟦mail:password⟧ ignorecertificateerrors true
imap.getmails result ♥mails sincedate ‴25/09/2019‴
imap.close

♥path = ‴♥environment⟦HOMEDRIVE⟧♥environment⟦HOMEPATH⟧\Documents\G1ANT.Robot\Automation Examples\mails.csv‴
⊂
System.IO.Directory.CreateDirectory(System.IO.Path.GetDirectoryName(♥path));
⊃
text.write filename ♥path text ‴‴

foreach ♥mail in ♥mails
    if ⊂♥mail⟦subject⟧.Contains("#robottest")⊃
        text.write filename ♥path text ‴♥mail⟦from⟧, ♥mail⟦subject⟧, ♥mail⟦content⟧♥newline‴ writemode append
    end if
end foreach

list.create text ♥path result ♥attachments
mail.smtp host ♥credential⟦mail:address⟧ login ♥credential⟦mail:address⟧ password ♥credential⟦mail:password⟧ from ♥credential⟦mail:address⟧ to ‴robotuser@g1ant.com‴ subject ‴Csv attachment‴ body ‴Hi, here's your mail info‴ attachments ♥attachments
```
