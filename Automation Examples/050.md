# Process Description

Open https://g1ant.com/ and scroll to the bottom of the page. Open excel [template](Fake%20Names%20for%20Form%20Filling.xlsx) and for each excel record, fill the contact form with the record data and click send.

# Steps to Automate

### 1. Open g1ant.com website and maximise the browser window

Make sure to enable addon selenium from the View/Addons panel.

```G1ANT
selenium.open chrome url g1ant.com
window ✱G1ANT✱ style maximize
```

### 2. Create a path variable containing the path to the "Fake Names for Form Filling.xlsx" template and open it

Don't forget to add a C# snippet which will create the directory for you.

```G1ANT
♥path = ‴♥environment⟦HOMEDRIVE⟧♥environment⟦HOMEPATH⟧\Documents\G1ANT.Robot\Automation Examples\Fake Names for Form Filling.xlsx‴
⊂
System.IO.Directory.CreateDirectory(System.IO.Path.GetDirectoryName(♥path));
⊃
xlsx.open ♥path
```

### 3. Scroll to the bottom of the page and get the number of excel records

The following code in the ‴‴ characters is a JavaScript function that will scroll to the bottom of the page.

```G1ANT
selenium.runscript ‴window.scrollTo(0, document.body.scrollHeight)‴
xlsx.countrows result ♥recordsNumber
```

### 4. Create a for loop in which you get row data

The easiest way is to create a procedure for it - GetRowData with the rowNumber parameter.

```G1ANT
for ♥rowNumber from 2 to ♥recordsNumber
    call GetRowData rowNumber ♥rowNumber
end for

procedure GetRowData rowNumber 2
    xlsx.getvalue row ♥rowNumber colindex 1 result ♥name
    xlsx.getvalue row ♥rowNumber colindex 2 result ♥surname
    xlsx.getvalue row ♥rowNumber colindex 3 result ♥email
    xlsx.getvalue row ♥rowNumber colindex 4 result ♥tag
    xlsx.getvalue row ♥rowNumber colindex 5 result ♥message
end procedure
```

### 5. Fill the name, surname and email fields in the contact form

Right click the "Name" field and choose "Inspect Element", right click the selected code in the developer tools and choose "Copy Xpath" that you can insert as below. Do the same with "Surname" and "Email" fields.

```G1ANT
    selenium.type ♥name search ‴//*[@id="contact"]/div/div[1]/div[1]/div/div/div[2]/form/div/div[1]/div/div[1]/input‴ by xpath
    selenium.type ♥surname search ‴//*[@id="contact"]/div/div[1]/div[1]/div/div/div[2]/form/div/div[1]/div/div[2]/input‴ by xpath
    selenium.type ♥email search ‴//*[@id="contact"]/div/div[1]/div[1]/div/div/div[2]/form/div/div[2]/div/div[1]/input‴ by xpath
```

### 6. Check all the options in the "select" HTML element and find one that is equal to the ♥tag variable

The `♥i` variable stores the number of currently processed option element.

The `♥isSameOption` variable stores information whether the option element is the same as the ♥tag variable.

`selenium.runscript` command returns here what the number of options is and sets it to the `♥optionsNumber` variable. Casting to integer is needed because in the while condition it is checked if the `♥i` variable is less than or equal to the `♥optionsNumber` variable. Also, the while loop stops when the `♥isSameOption` is true.

`selenium.gettext` returns the name of the option and it is checked later if it is the same as `♥tag`. If it is, `♥isSameOption` is set to true so the while loop will not have more iterations.

The `♥i` variable is increased by one with each iteration of the while loop because with each iteration, the next option is being processed.

```G1ANT
    ♥i = 1
    ♥isSameOption = false
    selenium.runscript ‴return document.getElementsByClassName("form-control")[3].getElementsByTagName("option").length‴ result ♥optionsNumber
    ♥optionsNumber = ⟦integer⟧♥optionsNumber
    
    while ⊂♥i <= ♥optionsNumber && !♥isSameOption⊃
        selenium.gettext search ‴//*[@id="contact"]/div/div[1]/div[1]/div/div/div[2]/form/div/div[2]/div/div[2]/select/option[♥i]‴ by xpath result ♥optionName
        
        if ⊂♥optionName == ♥tag⊃
            ♥isSameOption = true
        end if
        
        ♥i = ♥i + 1
    end while
```

### 7. When the name of the option is equal to ♥tag, select this correct option and press enter

This is what you should get. Copy XPath like in the previous steps. `⊂♥i-1⊃` means that it will be a number less than `♥i` by 1. So the down key will be pressed that many times.

```G1ANT
        if ⊂♥optionName == ♥tag⊃
            selenium.click search ‴//*[@id="contact"]/div/div[1]/div[1]/div/div/div[2]/form/div/div[2]/div/div[2]/select‴ by xpath
            keyboard ⋘down ⊂♥i-1⊃⋙
            keyboard ⋘enter⋙
            ♥isSameOption = true
        end if
```

### 8. Fill the message field in the contact form and click send

By copying XPaths this is what you should receive.

```G1ANT
    selenium.type ♥message search ‴//*[@id="contact"]/div/div[1]/div[1]/div/div/div[2]/form/div/div[3]/div/div/textarea‴ by xpath
    selenium.click search ‴//*[@id="contact"]/div/div[1]/div[1]/div/div/div[2]/form/div/div[4]/button‴ by xpath
```

### 9. Close the browser and the excel

```G1ANT
selenium.close
xlsx.close
```

# Final Code

```G1ANT
♥path = ‴♥environment⟦HOMEDRIVE⟧♥environment⟦HOMEPATH⟧\Documents\G1ANT.Robot\Automation Examples\Fake Names for Form Filling.xlsx‴
⊂
System.IO.Directory.CreateDirectory(System.IO.Path.GetDirectoryName(♥path));
⊃
selenium.open chrome url g1ant.com
window ✱G1ANT✱ style maximize
xlsx.open ♥path
selenium.runscript ‴window.scrollTo(0, document.body.scrollHeight)‴
xlsx.countrows result ♥recordsNumber

for ♥rowNumber from 2 to ♥recordsNumber
    call GetRowData rowNumber ♥rowNumber
    
    selenium.type ♥name search ‴//*[@id="contact"]/div/div[1]/div[1]/div/div/div[2]/form/div/div[1]/div/div[1]/input‴ by xpath
    selenium.type ♥surname search ‴//*[@id="contact"]/div/div[1]/div[1]/div/div/div[2]/form/div/div[1]/div/div[2]/input‴ by xpath
    selenium.type ♥email search ‴//*[@id="contact"]/div/div[1]/div[1]/div/div/div[2]/form/div/div[2]/div/div[1]/input‴ by xpath
    
    ♥i = 1
    ♥isSameOption = false
    selenium.runscript ‴return document.getElementsByClassName("form-control")[3].getElementsByTagName("option").length‴ result ♥optionsNumber
    ♥optionsNumber = ⟦integer⟧♥optionsNumber
    
    while ⊂♥i <= ♥optionsNumber && !♥isSameOption⊃
        selenium.gettext search ‴//*[@id="contact"]/div/div[1]/div[1]/div/div/div[2]/form/div/div[2]/div/div[2]/select/option[♥i]‴ by xpath result ♥optionName
        
        if ⊂♥optionName == ♥tag⊃
            selenium.click search ‴//*[@id="contact"]/div/div[1]/div[1]/div/div/div[2]/form/div/div[2]/div/div[2]/select‴ by xpath
            keyboard ⋘down ⊂♥i-1⊃⋙
            keyboard ⋘enter⋙
            ♥isSameOption = true
        end if
        
        ♥i = ♥i + 1
    end while
    
    selenium.type ♥message search ‴//*[@id="contact"]/div/div[1]/div[1]/div/div/div[2]/form/div/div[3]/div/div/textarea‴ by xpath
    selenium.click search ‴//*[@id="contact"]/div/div[1]/div[1]/div/div/div[2]/form/div/div[4]/button‴ by xpath
end for

selenium.close
xlsx.close

procedure GetRowData rowNumber 2
    xlsx.getvalue row ♥rowNumber colindex 1 result ♥name
    xlsx.getvalue row ♥rowNumber colindex 2 result ♥surname
    xlsx.getvalue row ♥rowNumber colindex 3 result ♥email
    xlsx.getvalue row ♥rowNumber colindex 4 result ♥tag
    xlsx.getvalue row ♥rowNumber colindex 5 result ♥message
end procedure
```
