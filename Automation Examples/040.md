# Process

Create an excel file with headers (Srno, Countries, Fert. Rate, Med. Age, Aggregate). Srno values count should start from 1. Open http://www.worldometers.info/world-population/population-by-country/ and extract data about countries 

# Steps to automate

# Final Code

```G1ANT
selenium.open type chrome url http://www.worldometers.info/world-population/population-by-country/
window ✱Population✱ style maximize
♥path = ‴C:\Users\dell\Desktop\Automation Examples\countries.xlsx‴
xlsx.open ♥path createifnotexist true

♥headers = Srno❚Countries❚Fert. Rate❚Med. Age❚Aggregate

call FillExcel list ♥headers

selenium.runscript ‴return document.getElementsByClassName("sorting_1").length‴ result ♥websiteRecordsNumber
♥websiteRecordsNumber = ⟦integer⟧♥websiteRecordsNumber
♥i = 1
♥excelRecordsNumber = 1

while ⊂♥i < ♥websiteRecordsNumber && ♥excelRecordsNumber < 16⊃

    selenium.gettext search ‴//*[@id="example2"]/tbody/tr[♥i]/td[9]‴ by xpath result ♥fertRate
    selenium.gettext search ‴//*[@id="example2"]/tbody/tr[♥i]/td[10]‴ by xpath result ♥medAge
    
    if ⊂!♥fertRate.Contains("N.A.") && !♥medAge.Contains("N.A.")⊃
        ♥fertRate = ⟦float⟧♥fertRate
        ♥medAge = ⟦float⟧♥medAge
    
        if ⊂♥fertRate > 2.5 && ♥medAge > 27⊃
            selenium.gettext search ‴//*[@id="example2"]/tbody/tr[♥i]/td[2]/a‴ by xpath result ♥country
            list.create ‴♥excelRecordsNumber❚♥country❚♥fertRate❚♥medAge❚⊂(♥fertRate*0.05)+♥medAge⊃‴ type text result ♥rowValues
            call FillExcel rowNumber ⊂♥excelRecordsNumber+1⊃ list ♥rowValues
            ♥excelRecordsNumber = ♥excelRecordsNumber + 1
        end if
    end if
    
    ♥i = ♥i + 1
end while

call StopScript

list.create text ♥path result ♥attachments
mail.smtp host ‴imap.gmail.com‴ login ‴user@gmail.com‴ password ‴p@$$w0rD‴ from ‴robot@gmail.com‴ to ‴robotuser@g1ant.com‴ subject ‴Csv attachment‴ body ‴Hi, here's your countries info‴ attachments ♥attachments

procedure FillExcel rowNumber 1 list ⟦list:text⟧a❚b❚c
    for ♥j from 1 to ⊂♥list.Count⊃
        xlsx.setvalue value ♥list⟦♥j⟧ row ♥rowNumber colindex ♥j
    end for
end procedure

procedure StopScript
    xlsx.close
    selenium.close
end procedure
```
