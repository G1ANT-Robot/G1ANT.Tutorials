# Process Description

Create an excel file with headers (Srno, Countries, Fert. Rate, Med. Age, Aggregate). Srno values count should start from 1. Open http://www.worldometers.info/world-population/population-by-country/ and extract data about first 15 countries that match the following conditions.

1. Fert. Rate is more than 2.5

2. Med. Age is more than 27

Then, insert Aggregate formulae to the excel and send an email with the attachment.

# Steps to Automate

### 1. Open the website and focus the Chrome window

Make sure to enable addon selenium from the View/Addons panel.

```G1ANT
selenium.open type chrome url http://www.worldometers.info/world-population/population-by-country/
window ✱Population✱ style maximize
```

### 2. Create an excel file and fill it with headers

The C# snippet `⊂♥headers.Count⊃` returns a number of `♥headers` elements. In each iteration of a loop, a header is appended to the excel. Using environment variables, the path will be correct on any computer.

Don't forget to add a C# snippet which will create the directory for you.

```G1ANT
♥path = ‴♥environment⟦HOMEDRIVE⟧♥environment⟦HOMEPATH⟧\Documents\G1ANT.Robot\Automation Examples\countries.xlsx‴
⊂
System.IO.Directory.CreateDirectory(System.IO.Path.GetFullPath(♥path));
⊃
xlsx.open ♥path createifnotexist true

♥headers = Srno❚Countries❚Fert. Rate❚Med. Age❚Aggregate

for ♥i from 1 to ⊂♥headers.Count⊃
    xlsx.setvalue value ♥headers⟦♥i⟧ row 1 colindex ♥i
end for
```

### 3. Get a number of records that are on the website

Right click any record's value that is in the first column and choose "Inspect Element", copy the class name and using JavaScript, get a number of its elements.

```G1ANT
selenium.runscript ‴return document.getElementsByClassName("sorting_1").length‴ result ♥websiteRecordsNumber
```

### 4. Create a loop with no body but with the correct conditions

`♥websiteRecordsNumber` must be of an integer structure because it will be compared to the `♥i` variable storing a number of a website record that is being currently processed. `♥excelRecordsNumber` is a number of records matchting the criteria from the "process description". So it is probably the best option here to use a while loop which should end when there are no more records on the website (`♥i` is equal to `♥websiteRecordsNumber`) or when `♥excelRowNumber` is 16 (because there are headers also and we want to have 15 records).

```G1ANT
♥websiteRecordsNumber = ⟦integer⟧♥websiteRecordsNumber
♥i = 1
♥excelRowNumber = 1

while ⊂♥i < ♥websiteRecordsNumber && ♥excelRowNumber <= 15⊃
    ♥i = ♥i + 1
end while
```

### 5. Get Fert. Rate and Med. Age values inside a loop

Right click on any Fert. Rate value and choose "Inspect Element", right click the selected code in the developer tools and choose "Copy Xpath" that you can insert as below, remembering to replace the current number of website record with the `♥i` variable. Do the same with Med. Age.

```G1ANT
    selenium.gettext search ‴//*[@id="example2"]/tbody/tr[♥i]/td[9]‴ by xpath result ♥fertRate
    selenium.gettext search ‴//*[@id="example2"]/tbody/tr[♥i]/td[10]‴ by xpath result ♥medAge
```

### 6. If a record matches the above cirteria, insert its values in the excel

Create an `if` block with a condition that compares `♥fertRate` and `♥medAge` with the criteria. In order for the C# snippet to work correctly, both variables must be of some numeric structure. Float is the most desired here as we are comparing numbers with decimal values.

Get a country name by copying xpath. Create a list containing all the values that should be in a row. Don't forget about the Aggregate value that you can calculate like below. Insert them into excel using a for loop. Increase `♥excelRecordsNumber` by 1.

```G1ANT
    ♥fertRate = ⟦float⟧♥fertRate
    ♥medAge = ⟦float⟧♥medAge

    if ⊂♥fertRate > 2.5 && ♥medAge > 27⊃
        selenium.gettext search ‴//*[@id="example2"]/tbody/tr[♥i]/td[2]/a‴ by xpath result ♥country
        list.create ‴♥excelRowNumber❚♥country❚♥fertRate❚♥medAge❚⊂(♥fertRate*0.05)+♥medAge⊃‴ type text result ♥rowValues
        
        for ♥i from 1 to ⊂♥rowValues.Count⊃
            xlsx.setvalue value ♥rowValues⟦♥i⟧ row 1 colindex ⊂♥excelRowNumber+1⊃
        end for
        
        ♥excelRowNumber = ♥excelRowNumber + 1
    end if
```

### 7. Refactor the code setting values in excel, create a procedure for it

Create a procedure `FillExcel` with the rowNumber parameter set to 1 by default and with the list parameter being of a list structure. Counter variable must be different than `♥i` because otherwise the procedure would change value of `♥i` in the while loop (number of currently processed website record).

```G1ANT
procedure FillExcel rowNumber 1 list ⟦list:text⟧a❚b❚c
    for ♥j from 1 to ⊂♥list.Count⊃
        xlsx.setvalue value ♥list⟦♥j⟧ row ♥rowNumber colindex ♥j
    end for
end procedure
```

Instead of the following code:

```G1ANT
for ♥i from 1 to ⊂♥headers.Count⊃
    xlsx.setvalue value ♥headers⟦♥i⟧ row 1 colindex ♥i
end for
```

Insert the following:

```G1ANT
call FillExcel list ♥headers
```

And instead of this:

```G1ANT
        for ♥i from 1 to ⊂♥rowValues.Count⊃
            xlsx.setvalue value ♥rowValues⟦♥i⟧ row 1 colindex ⊂♥excelRowNumber+1⊃
        end for
```

Insert this: 

```G1ANT
        call FillExcel rowNumber ⊂♥excelRowNumber+1⊃ list ♥rowValues
```

### 8. Close excel and Chrome

```G1ANT
xlsx.close
selenium.close
```

### 9. Resolve problem with casting to float when Fert. Rate and Med. Age are not numbers

After many iterations of a loop, suddenly `♥fertRate` and `♥medAge` are equal to "N.A." instead of some numbers. To resolve this, do the following.

```G1ANT
    if ⊂!♥fertRate.Contains("N.A.") && !♥medAge.Contains("N.A.")⊃
        ♥fertRate = ⟦float⟧♥fertRate
        ♥medAge = ⟦float⟧♥medAge
    
        if ⊂♥fertRate > 2.5 && ♥medAge > 27⊃
            selenium.gettext search ‴//*[@id="example2"]/tbody/tr[♥i]/td[2]/a‴ by xpath result ♥country
            list.create ‴♥excelRowNumber❚♥country❚♥fertRate❚♥medAge❚⊂(♥fertRate*0.05)+♥medAge⊃‴ type text result ♥rowValues
            call FillExcel rowNumber ⊂♥excelRowNumber+1⊃ list ♥rowValues
            ♥excelRowNumber = ♥excelRowNumber + 1
        end if
    end if
```

### 10. Send an email with the .xlsx file attachment. 

It is a good practice to store usernames and passwords in Credential Container. In order to add a credential, choose Tools/Credential Container..., click "Add" and in the "Key" field insert the name of your value, for example "mail:address". Remember to have a key containing the `:` sign. In the "Value" and "Retype value" fields, insert for example your mail address. To the same with a mail password.

See below how to access those values using the `♥credential` special variable.

```G1ANT
list.create text ♥path result ♥attachments
mail.smtp host ‴poczta.o2.pl‴ login ♥credential⟦mail:address⟧ password ♥credential⟦mail:password⟧ from ♥credential⟦mail:address⟧ to ‴robotuser@g1ant.com‴ subject ‴Csv attachment‴ body ‴Hi, here's your countries info‴ attachments ♥attachments
```

# Final Code

```G1ANT
selenium.open type chrome url http://www.worldometers.info/world-population/population-by-country/
window ✱Population✱ style maximize
♥path = ‴♥environment⟦HOMEDRIVE⟧♥environment⟦HOMEPATH⟧\Documents\G1ANT.Robot\Automation Examples\countries.xlsx‴
⊂
System.IO.Directory.CreateDirectory(System.IO.Path.GetFullPath(♥path));
⊃
xlsx.open ♥path createifnotexist true

♥headers = Srno❚Countries❚Fert. Rate❚Med. Age❚Aggregate

call FillExcel list ♥headers

selenium.runscript ‴return document.getElementsByClassName("sorting_1").length‴ result ♥websiteRecordsNumber
♥websiteRecordsNumber = ⟦integer⟧♥websiteRecordsNumber
♥i = 1
♥excelRowNumber = 1

while ⊂♥i < ♥websiteRecordsNumber && ♥excelRowNumber <= 15⊃

    selenium.gettext search ‴//*[@id="example2"]/tbody/tr[♥i]/td[9]‴ by xpath result ♥fertRate
    selenium.gettext search ‴//*[@id="example2"]/tbody/tr[♥i]/td[10]‴ by xpath result ♥medAge
    
    if ⊂!♥fertRate.Contains("N.A.") && !♥medAge.Contains("N.A.")⊃
        ♥fertRate = ⟦float⟧♥fertRate
        ♥medAge = ⟦float⟧♥medAge
    
        if ⊂♥fertRate > 2.5 && ♥medAge > 27⊃
            selenium.gettext search ‴//*[@id="example2"]/tbody/tr[♥i]/td[2]/a‴ by xpath result ♥country
            list.create ‴♥excelRowNumber❚♥country❚♥fertRate❚♥medAge❚⊂(♥fertRate*0.05)+♥medAge⊃‴ type text result ♥rowValues
            call FillExcel rowNumber ⊂♥excelRowNumber+1⊃ list ♥rowValues
            ♥excelRowNumber = ♥excelRowNumber + 1
        end if
    end if
    
    ♥i = ♥i + 1
end while

xlsx.close
selenium.close

list.create text ♥path result ♥attachments
mail.smtp host ‴poczta.o2.pl‴ login ♥credential⟦mail:address⟧ password ♥credential⟦mail:password⟧ from ♥credential⟦mail:address⟧ to ‴robotuser@g1ant.com‴ subject ‴Csv attachment‴ body ‴Hi, here's your countries info‴ attachments ♥attachments

procedure FillExcel rowNumber 1 list ⟦list:text⟧a❚b❚c
    for ♥j from 1 to ⊂♥list.Count⊃
        xlsx.setvalue value ♥list⟦♥j⟧ row ♥rowNumber colindex ♥j
    end for
end procedure
```
