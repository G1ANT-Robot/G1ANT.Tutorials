# Process Description

Open http://www.bugabong.com/, select the business category, extract the number of votes and book names of first 10' products listed. Send an email with a .csv file containing all the data.

# Steps to Automate

### 1. Open website

Make sure to enable addon selenium from the View/Addons panel.

```G1ANT
selenium.open chrome url http://www.bugabong.com/
```

### 2. Open the business category

Right click any product's category that is "business" and choose "Inspect Element", right click the selected code in the developer tools and choose "Copy Xpath" that you can insert as below.

```G1ANT
selenium.click search ‴//*[@id="books"]/li[1]/div/div[2]/p/a‴ by xpath
```

### 3. Get current url (business category) and create empty lists for book names and votes numbers

```G1ANT
selenium.geturl result ♥mainUrl
list.create type text result ♥bookNames
list.create type integer result ♥votesNumbers
```

### 4. Open the first book result, get a book name and a vote number

Repeat the steps from the 2nd point to get correct xpaths.

```G1ANT
selenium.click search ‴//*[@id="books"]/li[1]/div/h3/a‴ by xpath
selenium.gettext search ‴//*[@id="book-info"]/h1‴ by xpath result ♥bookName
selenium.gettext search ‴//*[@id="book-info"]/div[2]/span[1]‴ by xpath result ♥votesNumber
```

### 5. Add extracted values to the lists and process them so that they are of correct structure

```G1ANT
list.add ♥bookNames toadd ♥bookName
list.add ♥votesNumbers toadd ⊂♥votesNumber.Replace("Votes: ", "")⊃
```

### 6. Create a for loop, change id of opening book pages and set url to the business page at the end

```G1ANT
for ♥i from 1 to 10
    selenium.click search ‴//*[@id="books"]/li[♥i]/div/h3/a‴ by xpath
    selenium.gettext search ‴//*[@id="book-info"]/h1‴ by xpath result ♥bookName
    list.add ♥bookNames toadd ♥bookName
    selenium.gettext search ‴//*[@id="book-info"]/div[2]/span[1]‴ by xpath result ♥votesNumber
    list.add ♥votesNumbers toadd ⊂♥votesNumber.Replace("Votes: ", "")⊃
    selenium.seturl ♥mainUrl
end for
```

### 7. Close the browser

```G1ANT
selenium.close
```

### 8. Create a path to the Books.csv file

Don't forget to add a C# snippet which will create the directory for you.

```G1ANT
♥path = ‴♥environment⟦HOMEDRIVE⟧♥environment⟦HOMEPATH⟧\Documents\G1ANT.Robot\Automation Examples\Books.csv‴

⊂
System.IO.Directory.CreateDirectory(System.IO.Path.GetFullPath(♥path));
⊃
```

### 9. Based on loop iteration number append or override the file

```G1ANT
for ♥i from 1 to ⊂♥bookNames.Count⊃
    text.write filename ♥path text ‴♥bookNames⟦♥i⟧, ♥votesNumbers⟦♥i⟧♥newline‴ writemode ⊂♥i > 1 ? "append" : "override"⊃
end for
```

### 10. Send an email with the .csv file attachment. Make sure to fill all the email values correctly

```G1ANT
list.create text ♥path result ♥attachments
mail.smtp host ‴imap.gmail.com‴ login ‴user@gmail.com‴ password ‴p@$$w0rD‴ from ‴robot@gmail.com‴ to ‴robotuser@g1ant.com‴ subject ‴Csv attachment‴ body ‴Hi, here's your book info‴ attachments ♥attachments
```

# Final Code

```G1ANT
selenium.open chrome url http://www.bugabong.com/
selenium.click search ‴//*[@id="books"]/li[1]/div/div[2]/p/a‴ by xpath
selenium.geturl result ♥mainUrl
list.create type text result ♥bookNames
list.create type integer result ♥votesNumbers

for ♥i from 1 to 10
    selenium.click search ‴//*[@id="books"]/li[♥i]/div/h3/a‴ by xpath
    selenium.gettext search ‴//*[@id="book-info"]/h1‴ by xpath result ♥bookName
    list.add ♥bookNames toadd ♥bookName
    selenium.gettext search ‴//*[@id="book-info"]/div[2]/span[1]‴ by xpath result ♥votesNumber
    list.add ♥votesNumbers toadd ⊂♥votesNumber.Replace("Votes: ", "")⊃
    selenium.seturl ♥mainUrl
end for

selenium.close

♥path = ‴♥environment⟦HOMEDRIVE⟧♥environment⟦HOMEPATH⟧\Documents\G1ANT.Robot\Automation Examples\Books.csv‴
⊂
System.IO.Directory.CreateDirectory(System.IO.Path.GetFullPath(♥path));
⊃

for ♥i from 1 to ⊂♥bookNames.Count⊃
    text.write filename ♥path text ‴♥bookNames⟦♥i⟧, ♥votesNumbers⟦♥i⟧♥newline‴ writemode ⊂♥i > 1 ? "append" : "override"⊃
end for

list.create text ♥path result ♥attachments
mail.smtp host ‴imap.gmail.com‴ login ‴user@gmail.com‴ password ‴p@$$w0rD‴ from ‴robot@gmail.com‴ to ‴robotuser@g1ant.com‴ subject ‴Csv attachment‴ body ‴Hi, here's your book info‴ attachments ♥attachments
```
