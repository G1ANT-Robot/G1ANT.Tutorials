# Process

Open https://www.google.com/finance?hl=en and extract World Markets Table, insert the data into excel.

# Steps to automate

##### 1. Open website and maximise the browser window

Make sure to enable addon selenium from the View/Addons panel.

```G1ANT
selenium.open chrome url https://www.google.com/finance?hl=en
window ✱Google✱ style maximize
```

##### 2. Open excel

Make sure to enable addon xlsx from the View/Addons panel and specify the correct path.

```G1ANT
xlsx.open ‴C:\Users\dell\Desktop\Automation Examples\World Markets Table.xlsx‴ createifnotexist true
```

##### 3. Create a for loop which will iterate through all 5 records

```G1ANT
for ♥i from 1 to 5

end for
```

##### 4. Get all World Markets Table values and insert them into excel

Right click some value and choose "Inspect Element", right click the selected code in the developer tools and choose "Copy Xpath" that you can insert as below. Do the same with all other values in the table.

As you may notice all of the xpaths share the same base xpath so in order for your script to look cleaner, you can create a helpful variable for example `♥baseXpath`. You can also notice that in the xpath, there is only one number that changes with each row, you can insert there the `♥i` variable.

The `selenium.gettext` command returns its result in the `♥result` variable so you can use it in the `xlsx.setvalue` command.

```G1ANT
    ♥baseXpath = ‴//*[@id="kp-wp-tab-MARKET_SUMMARY"]/div[2]/div/div[2]/div[2]/div/div/div/div[♥i]/g-link/a/div‴
    selenium.gettext search ‴♥baseXpath/div[1]/div‴ by xpath
    xlsx.setvalue ♥result row ♥i colindex 1
    selenium.gettext search ‴♥baseXpath/div[2]/span[1]‴ by xpath
    xlsx.setvalue ♥result row ♥i colindex 2
    selenium.gettext search ‴♥baseXpath/div[1]/span/span‴ by xpath
    xlsx.setvalue ♥result row ♥i colindex 3
    selenium.gettext search ‴♥baseXpath/div[2]/span[2]/span[1]‴ by xpath
    xlsx.setvalue ♥result row ♥i colindex 4
    selenium.gettext search ‴♥baseXpath/div[2]/span[2]/span[2]/span‴ by xpath
    xlsx.setvalue ♥result row ♥i colindex 5
```

##### 5. Close excel and the browser

```G1ANT
xlsx.close
selenium.close
```

# Final Code

```G1ANT
selenium.open chrome url https://www.google.com/finance?hl=en
window ✱Google✱ style maximize
xlsx.open ‴C:\Users\dell\Desktop\Automation Examples\World Markets Table.xlsx‴ createifnotexist true

for ♥i from 1 to 5
    ♥baseXpath = ‴//*[@id="kp-wp-tab-MARKET_SUMMARY"]/div[2]/div/div[2]/div[2]/div/div/div/div[♥i]/g-link/a/div‴
    selenium.gettext search ‴♥baseXpath/div[1]/div‴ by xpath
    xlsx.setvalue ♥result row ♥i colindex 1
    selenium.gettext search ‴♥baseXpath/div[2]/span[1]‴ by xpath
    xlsx.setvalue ♥result row ♥i colindex 2
    selenium.gettext search ‴♥baseXpath/div[1]/span/span‴ by xpath
    xlsx.setvalue ♥result row ♥i colindex 3
    selenium.gettext search ‴♥baseXpath/div[2]/span[2]/span[1]‴ by xpath
    xlsx.setvalue ♥result row ♥i colindex 4
    selenium.gettext search ‴♥baseXpath/div[2]/span[2]/span[2]/span‴ by xpath
    xlsx.setvalue ♥result row ♥i colindex 5
end for

xlsx.close
selenium.close
```
