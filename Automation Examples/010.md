# Process

Open http://nseindia.com/corporates/content/eq_research_reports_listed.htm and for the first 50 companies, download a recent month’s report. Save it in a separate folder **Today’s date/Company Name/PDF Name.pdf**

# Steps to automate

#### 1. Create variables - `url` storing the link given and `basePath` containing the path to the main folder

```G1ANT
♥url = http://nseindia.com/corporates/content/eq_research_reports_listed.htm
♥basePath = C:\Users\dell\Desktop\PDF Automation
```

#### 2. Create a main procedure in which you open the website and call it after the initialised variables section

```G1ANT
call DownloadPdfFiles

procedure DownloadPdfFiles
    selenium.open chrome url ♥url
end procedure
```

#### 3. Run the script and investigate the website yourself, open developer tools and find the HTML elements that are important (company names and the buttons to open their webpages)

After a quick investigation you should see that the company names are in the third column and you can get the first records by running these scripts in the developer tools console:

```js
document.getElementsByClassName("normalText")[2].innerText
document.getElementsByClassName("normalText")[6].innerText
document.getElementsByClassName("normalText")[10].innerText
```
Getting the buttons to open company webpages is quite similar. Here are examples of how you can open the first three of them:

```js
document.getElementsByClassName("normalText")[3].getElementsByTagName("a")[0].click()
document.getElementsByClassName("normalText")[7].getElementsByTagName("a")[0].click()
document.getElementsByClassName("normalText")[11].getElementsByTagName("a")[0].click()
```

You can notice that the index for each record increases by 4 and that the respective buttons' indexes are greater by 1 than the company names' indexes.

#### 4. Based on your research, create a for loop in which you will wait for the elements to be loaded, get a company name and then open its webpage. 

```G1ANT
    for ♥i from 2 to 198 step 4
        selenium.waitforvalue ‴return document.getElementsByClassName("normalText").length > 0‴ expectedvalue true
        selenium.runscript ‴return document.getElementsByClassName("normalText")[♥i].innerText‴
        ♥companyName = ♥result
        selenium.runscript ‴document.getElementsByClassName("normalText")[♥i+1].getElementsByTagName("a")[0].click()‴
    end for
```

This for loop will create an `i` variable which will be initialised with 2 and increased by 4 with each iteration of a loop until the `i` variable reaches 198 because we want to get only the first 50 companies (so you can calculate it where 198 number is from: 2+4*49). 

`selenium.waitforvalue` command is used here to wait for "normalText" class elements to be loaded, `selenium.runscript` command runs javascript code that you tested in your browser. The `companyName` variable stores what this command has returned. 

#### 5. After you have opened a company webpage, wait for the elements to be loaded and click the first .pdf file in the table to download it. 

After a quick investigation, you should see that opening the first .pdf file on this webpage is possible by running this script:

```js
document.getElementsByClassName("date")[2].getElementsByTagName("a")[0].click()
```

But obviously you remember about waiting for these class elements to be loaded so first you add this line of code:

```G1ANT
        selenium.waitforvalue ‴return document.getElementsByClassName("date").length > 0‴ expectedvalue true
```

And then you open the .pdf file like this:

```G1ANT
        selenium.runscript ‴document.getElementsByClassName("date")[2].getElementsByTagName("a")[0].click()‴
```

#### 6. The .pdf file has been opened in the second tab so save it under the correct folder (see the process description)

```G1ANT
        ♥dateformat = yyyy-MM-dd
        ♥path = ♥basePath\♥date\♥companyName
        keyboard ⋘CTRL+S⋙
        ⊂
        System.IO.Directory.CreateDirectory(♥path);
        ⊃
        keyboard ⋘CTRL+C⋙
        ♥clipboard = ♥path\♥clipboard
        keyboard ⋘CTRL+V⋙
        keyboard ⋘ENTER⋙
```

The `dateformat` variable is a special variable which specifies the format of the today date which is stored by the `date` special variable. As we want to process 50 companies, it is nice to have this script execute each step as fast as it can so we use clipboard for that instead of typing everything key by key. To save this file, we use Ctrl+S shortcut and we create a directory using a C# snippet for this file. Using Ctrl+C shortcut we copy the name of the .pdf file (it is already selected), add the full path to the .pdf file to the `clipboard` variable, paste it using Ctrl+V shortcut and press Enter. 

#### 7. Close the tab, go back to the main webpage and close the for block

```G1ANT
        selenium.closetab
        selenium.seturl ♥url
    end for 
```

#### 8. Close the selenium in between the `end for` command and `end procedure` command

```G1ANT
    end for
    
    selenium.close
end procedure
```

#### 9. After running this script multiple times you notice the first issue - sometimes "Confirm Save As" window appears when you download the same file again. Fix this

After pressing Enter add the following workaround.

```G1ANT
        try errorcall Empty
            window ‴✱Confirm Save As✱‴
            keyboard ⋘LEFT⋙⋘ENTER⋙
        end try
```

If the window is displayed, click "yes" to save the file. If the window is not displayed, the script would fail so you surround this code with `try` block, add errorcall argument to it so that it runs smoothly without showing any error messages. Add the "Empty" procedure at the end of your script.

```G1ANT
procedure Empty
end procedure
```

#### 10. Sometimes you encounter the second problem which is that some company webpages cannot be loaded, the loading wheel is moving and doesn't stop, the `selenium.waitforvalue` command throws timeout exception - fix this issue

So you modify this line:

```G1ANT
        selenium.waitforvalue ‴return document.getElementsByClassName("date").length > 0‴ expectedvalue true
```

And add errorjump argument to it:

```G1ANT
        selenium.waitforvalue ‴return document.getElementsByClassName("date").length > 0‴ expectedvalue true errorjump MoveToNextCompany
```

And specify the label before `selenium.seturl` command

```G1ANT
        label MoveToNextCompany
```

# Final Code

```G1ANT
addon selenium version 4.100.19170.929
addon core version 4.100.19170.929
addon language version 4.100.19163.1112

♥url = http://nseindia.com/corporates/content/eq_research_reports_listed.htm
♥basePath = C:\Users\dell\Desktop\Automation Anywhere

call DownloadPdfFiles

procedure DownloadPdfFiles
    selenium.open chrome url ♥url
    
    for ♥i from 2 to 198 step 4
        selenium.waitforvalue ‴return document.getElementsByClassName("normalText").length > 0‴ expectedvalue true
        selenium.runscript ‴return document.getElementsByClassName("normalText")[♥i].innerText‴
        ♥companyName = ♥result
        selenium.runscript ‴document.getElementsByClassName("normalText")[♥i+1].getElementsByTagName("a")[0].click()‴
        selenium.waitforvalue ‴return document.getElementsByClassName("date").length > 0‴ expectedvalue true errorjump MoveToNextCompany
        selenium.runscript ‴document.getElementsByClassName("date")[2].getElementsByTagName("a")[0].click()‴
        ♥dateformat = yyyy-MM-dd
        ♥path = ♥basePath\♥date\♥companyName
        keyboard ⋘CTRL+S⋙
        ⊂
        System.IO.Directory.CreateDirectory(♥path);
        ⊃
        keyboard ⋘CTRL+C⋙
        ♥clipboard = ♥path\♥clipboard
        keyboard ⋘CTRL+V⋙
        keyboard ⋘ENTER⋙
        
        try errorcall Empty
            window ‴✱Confirm Save As✱‴
            keyboard ⋘LEFT⋙⋘ENTER⋙
        end try
        
        selenium.closetab
        label MoveToNextCompany
        selenium.seturl ♥url
    end for
    
    selenium.close
end procedure

procedure Empty
end procedure
```

Don't forget to select selenium addon in the Addons panel (View/Addons).
