# Process

Open https://www.vat19.com/, go to fun & strange stuff and download first 20 products' information (name, price, image) and save it into excel file. Template is available [here](Products%20Template.xlsx).

# Steps to automate

#### 1. Specify a path to your folder, open website and maximise the browser

```G1ANT
♥basePath = C:\Users\dell\Desktop\Automation Examples
selenium.open chrome url https://www.vat19.com/
window ✱Unique✱ style maximize
```

Use window command and part of the window title as an argument to bring it to the front and maximise.

#### 2. Go to fun & strange stuff, create lists which will contain product names and their prices

After running the above script and seeing the webpage, you can inspect the button element and in the developer tools, right click it and choose "Copy Xpath" that you can insert in the search argument for selenium.click command.

```G1ANT
selenium.click search ‴//*[@id="side-nav-content"]/div[1]/ul/li[3]/a‴ by xpath
list.create type text result ♥products
list.create type text result ♥prices
```

#### 3. Wait for elements to be loaded, get link to the first image, get name of the product and price value and store them in the lists

Right click the image element and see that the "src" attribute stores a link to it. The below JavaScript code returns a link to the first image. You can run it in the browser's console to check it. 

```js
document.getElementsByClassName("lazyautosizes lazyloaded")[0].getAttribute("src")
```

So let's wait for this element using the below G1ANT code.

```G1ANT
selenium.waitforvalue ‴return document.getElementsByClassName("lazyautosizes lazyloaded").length > 0‴ expectedvalue true
```

After right clicking the product name and price, you may see that geting those values is as easy as below. 

```G1ANT
selenium.runscript ‴return document.getElementsByClassName("productSketch-name-link")[0].innerText‴ result ♥productName
list.add ♥products toadd ♥productName
selenium.runscript ‴return document.getElementsByClassName("regularPrice")[0].innerText‴ result ♥price
list.add ♥prices toadd ♥price
```

#### 4. Download the first image

In order to do this, add "https:" prefix to the "src" and use file.download command.

```G1ANT
♥imageSrc = ⊂"https:" + ♥imageSrc⊃
file.download ♥imageSrc filename ♥basePath\1.png
```

#### 5. Put all above steps in a loop

This is what you should get:

```G1ANT
for ♥i from 0 to 19
    selenium.runscript ‴return document.getElementsByClassName("lazyautosizes lazyloaded")[♥i].getAttribute("src")‴ result ♥imageSrc
    ♥imageSrc = ⊂"https:" + ♥imageSrc⊃
    file.download ♥imageSrc filename ♥basePath\⊂♥i+1⊃.png
    selenium.runscript ‴return document.getElementsByClassName("productSketch-name-link")[♥i].innerText‴ result ♥productName
    list.add ♥products toadd ♥productName
    selenium.runscript ‴return document.getElementsByClassName("regularPrice")[♥i].innerText‴ result ♥price
    list.add ♥prices toadd ♥price
end for
```

#### 6. After running this script, you may notice that it fails after some time

It does so because webpage isn't fully loaded and some of the elements are not yet present so the script can't continue. To load more elements, you should scroll. Insert below line at the end of the loop to achieve this.

```G1ANT
    selenium.runscript ‴window.scrollBy(0, 200)‴
```

#### 7. Close the browser and open excel template

```G1ANT
selenium.close

excel.open ‴♥basePath\Images Template.xlsx‴
```

#### 8. Create a loop, insert images, products and prices

After pressing alt in the excel application, some of the letters appear which indicates what shortcut to use if you want to open some specific option from the menu. In this case, we need to press "n" and "p" to insert an image. Using keyboard to insert long texts letter by letter is sometimes not the fastest option so let's use clipboard for that and Ctrl+V shortuct to paste it in the "Save As" window. Then, set values for product name and price. Lastly, go the first column, next row by using Home and Down keys.

```G1ANT
for ♥i from 1 to 20
    keyboard ⋘alt⋙np
    ♥clipboard = ♥basePath\♥i.png
    keyboard ⋘CTRL+V⋙
    keyboard ⋘enter⋙
    excel.setvalue value ♥products⟦♥i⟧ row ⊂♥i+1⊃ colname b
    excel.setvalue value ♥prices⟦♥i⟧ row ⊂♥i+1⊃ colname c
    keyboard ⋘HOME⋙⋘DOWN⋙
end for
```

#### 9. After running this script, you see that sometimes images are bigger than the cell, so make them smaller

Open "Format" (j+p) and in the "Size" section, in "Height" field (h), insert 2.75 so that all images are the same size. Place the below script after inserting an image. 

```G1ANT
    keyboard ⋘alt⋙⋘j+p⋙h
    keyboard 2.75⋘enter⋙
```

# Final Code

```G1ANT
addon selenium version 4.101.0.0
addon msoffice version 4.101.0.0
addon core version 4.101.0.0
addon language version 4.100.19163.1112

♥basePath = C:\Users\dell\Desktop\Automation Examples
selenium.open chrome url https://www.vat19.com/
window ✱Unique✱ style maximize
selenium.click search ‴//*[@id="side-nav-content"]/div[1]/ul/li[3]/a‴ by xpath
list.create type text result ♥products
list.create type text result ♥prices
selenium.waitforvalue ‴return document.getElementsByClassName("lazyautosizes lazyloaded").length > 0‴ expectedvalue true

for ♥i from 0 to 10
    selenium.runscript ‴return document.getElementsByClassName("lazyautosizes lazyloaded")[♥i].getAttribute("src")‴ result ♥imageSrc
    ♥imageSrc = ⊂"https:" + ♥imageSrc⊃
    file.download ♥imageSrc filename ♥basePath\⊂♥i+1⊃.png
    selenium.runscript ‴return document.getElementsByClassName("productSketch-name-link")[♥i].innerText‴ result ♥productName
    list.add ♥products toadd ♥productName
    selenium.runscript ‴return document.getElementsByClassName("regularPrice")[♥i].innerText‴ result ♥price
    list.add ♥prices toadd ♥price
    selenium.runscript ‴window.scrollBy(0, 200)‴
end for

selenium.close

excel.open ‴♥basePath\Images Template.xlsx‴

for ♥i from 1 to 5
    keyboard ⋘alt⋙np
    ♥clipboard = ‴C:\Users\dell\Desktop\♥i.png‴
    keyboard ⋘CTRL+V⋙
    keyboard ⋘enter⋙
    keyboard ⋘alt⋙⋘j+p⋙h
    keyboard 2.75⋘enter⋙
    excel.setvalue value ♥products⟦♥i⟧ row ⊂♥i+1⊃ colname b
    excel.setvalue value ♥prices⟦♥i⟧ row ⊂♥i+1⊃ colname c
    keyboard ⋘HOME⋙⋘DOWN⋙
end for

excel.save ‴♥basePath\Products Info.xlsx‴
excel.close
```
